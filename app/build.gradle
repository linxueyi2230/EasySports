apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao' // apply plugin

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId "com.rayhahah.easysports"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 6
        versionName "1.1.5"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        ndk {
            abiFilters "armeabi", "armeabi-v7a","x86"
        }

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
            }
        }

    }
    signingConfigs {
        config {
            storeFile file('../doc/wedo.jks')
            storePassword 'wedo2017'
            keyAlias "wedo"
            keyPassword 'wedo2017'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
//            useProguard false
            minifyEnabled false
            signingConfig signingConfigs.config
        }


        applicationVariants.all { variant ->
            if (variant.buildType.name.equals('release')) {
                variant.outputs.all { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        //如果不是jenkins就不用修改路径
                        if ("false".equals(IS_JENKINS)) {
                            def fileName = "EasySport_${defaultConfig.versionName}_${releaseTime()}_${variant.flavorName}.apk"
                            outputFileName = new File("/local", fileName)
//                            output.outputFile = new File(outputFile.parent, fileName)
                        } else {
                            def fileName = "EasySport_${RELEASE_TIME}_${defaultConfig.versionName}_${PRODUCT_FLAVOR}_.apk"
                            //服务器上的路径
                            //适配AS3.0
                            outputFileName = new File("/upload", fileName)
//                            output.outputFile = new File("E:/android/apk/easysports/upload", fileName)
                        }
                    }
                }
            } else {
                variant.outputs.all { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        //如果是jenkins就修改路径
                        if ("true".equals(IS_JENKINS)) {
                            def fileName = "EasySport_${RELEASE_TIME}_${defaultConfig.versionName}_${PRODUCT_FLAVOR}_debug.apk"
                            //服务器上的路径
                            //适配AS3.0
                            outputFileName = new File("/upload", fileName)
//                            output.outputFile = new File("E:/android/apk/easysports/upload", fileName)
                        }
                    }
                }
            }
        }
    }

    dataBinding {
        enabled = true
    }

    //使AS知道.资源文件目录的位置
    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res'
            ]
        }
    }

    greendao {
        schemaVersion 5
        daoPackage 'com.rayhahah.greendao.gen'
        targetGenDir 'src/main/java'
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    aaptOptions {
        cruncherEnabled = false
        useNewCruncher = false
    }

    //增加编译内存到4g
    dexOptions{
        incremental false
        javaMaxHeapSize "4g"
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    flavorDimensions "default"
    productFlavors {
        official {
            dimension "default"
            manifestPlaceholders = [CHANNEL: "OFFICIAL"]
        }
        xiaomi {
            dimension "default"
            manifestPlaceholders = [CHANNEL: "XIAOMI"]
        }
        yingyongbao {
            dimension "default"
            manifestPlaceholders = [CHANNEL: "YINGYONGBAO"]
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation(name: 'hotfix_core-release-3.1.0', ext: 'aar')
    implementation(name: 'LiteAVSDK_Smart_3.3.1655', ext: 'aar')
    //    implementation(project(path: ':rbase')) {
    //        exclude group: 'com.squareup.okhttp3'
    //        exclude module: 'okio'
    //        exclude module: 'gson'
    //    }
    implementation project(path: ':rbase')
    implementation project(path: ':rvideoplayer')
    implementation project(path: ':dialoglib')
    //如果你想应用能够兼容Android6.0，请添加此依赖(org.apache.http.legacy.jar)
    implementation files('libs/MobCommons-2017.0608.1618.jar')
    implementation files('libs/MobTools-2017.0608.1618.jar')
    implementation files('libs/ShareSDK-Core-3.0.0.jar')
    implementation files('libs/ShareSDK-QQ-3.0.0.jar')
    implementation files('libs/ShareSDK-QZone-3.0.0.jar')
    implementation files('libs/ShareSDK-SinaWeibo-3.0.0.jar')
    implementation files('libs/ShareSDK-Wechat-3.0.0.jar')
    implementation files('libs/ShareSDK-Wechat-Core-3.0.0.jar')
    implementation files('libs/ShareSDK-Wechat-Moments-3.0.0.jar')
    implementation files('libs/zxing.jar')
    implementation files('libs/alicloud-android-utils-1.0.3.jar')
    implementation files('libs/utdid4all-1.1.5.3_proguard.jar')
    //    阿里云热修复远程依赖失败
    //    implementation 'com.aliyun.ams:alicloud-android-hotfix:3.1.0'
    implementation 'com.tencent.sonic:sdk:2.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:multidex:1.0.2'
    implementation 'org.greenrobot:greendao:3.2.2'
    // add library
    //    腾讯buglySDK
    implementation 'com.tencent.bugly:crashreport:2.6.6'
    //    腾讯buglyNDK
    implementation 'com.tencent.bugly:nativecrashreport:3.3.1'
    testImplementation 'junit:junit:4.12'
    annotationProcessor 'com.jakewharton:butterknife:7.0.1'
}
